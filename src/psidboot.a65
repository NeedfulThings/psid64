;   psid64 - create a C64 executable from a PSID file
;   Copyright (C) 2001  Roland Hermans <rolandh@users.sourceforge.net>
;
;   This program is free software; you can redistribute it and/or modify
;   it under the terms of the GNU General Public License as published by
;   the Free Software Foundation; either version 2 of the License, or
;   (at your option) any later version.
;
;   This program is distributed in the hope that it will be useful,
;   but WITHOUT ANY WARRANTY; without even the implied warranty of
;   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;   GNU General Public License for more details.
;
;   You should have received a copy of the GNU General Public License
;   along with this program; if not, write to the Free Software
;   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

r1_org	= $0002

#define MAX_BLOCKS			4

	.word $0801

	* = $0801

	.word basic
	.word 2001
	.byte $9e,"2061",0
basic	.word 0


	jmp start

r1_src	= *
	* = r1_org

	; Parameters
src	.word 0
dest	.word 0
counter	.byte 0
zp	.word 0
numblk	.byte 0
blocks	.dsb 4*MAX_BLOCKS, 0
chars	.byte 0
player	.word 0

memmove	.(
	; Move all blocks to highest memory area
	ldy #0
l1	dec src+1
	dec dest+1
l2	lda (src),y
	sta (dest),y
	iny
	bne l2
	dec counter
	bne l1

	; Move the blocks to the correct locations
	ldx numblk
loop	lda blocks,x
	sta dest
	lda blocks+MAX_BLOCKS,x
	sta dest+1
	ldy #0
	lda blocks+3*MAX_BLOCKS,x
	beq skip1
	sta counter
copy1	lda (zp),y
	sta (dest),y
	iny
	bne copy1
	inc zp+1
	inc dest+1
	dec counter
	bne copy1
skip1	lda blocks+2*MAX_BLOCKS,x
	beq skip2
	sta counter
copy2	lda (zp),y
	sta (dest),y
	iny
	cpy counter
	bne copy2
	tya
	clc
	adc zp
	sta zp
	bcc skip2
	inc zp+1
skip2	dex
	bpl loop

	; Copy character ROM data for screens $4000-$8000 or $C000-$D000
	lda chars
	beq nochars
	sta dest+1
	lda #$d8
	sta zp+1
	ldy #0
	sty zp
	sty dest
	ldx #7
	dec 1				; $33
copy3	lda (zp),y
	sta (dest),y
	iny
	bne copy3
	inc zp+1
	inc dest+1
	dex
	bpl copy3

nochars	lda #$37
	sta 1
	jmp (player)
	.)

r1_len	= *-r1_org
	* = r1_src+r1_len


start	.(
	sei
	ldx #$ff
	txs
	cld
	lda #$37
	sta 1
	jsr $ff84

	; Initialize VIC-II chip
	ldx #$2d
vicinit	lda vicdata,x
	sta $d000,x
	dex
	bpl vicinit

	; Initialize SID chip. The volume setting is done in psiddrv.a65.
	ldx #$17
	lda #0
sidinit	sta $d400,x
	dex
	bpl sidinit

	; Initialize color memory
	tay
colinit	sta $d800,y
	sta $d900,y
	sta $da00,y
	sta $db00,y
	iny
	bne colinit

	ldx #29				; set colors of program name
	lda #15
prgcol	sta $d800+45,x
	dex
	bpl prgcol
	ldx #14
l1	lda linecol,x
	sta $d804,x
	dex
	bpl l1

	ldy #240			; set color of information lines
l2	ldx #39
	lda #1
l3	cpx #9
	beq l4
	bcs l5
	lda #13
	.byte $2c			; bit $xxxx
l4	lda #7
l5	dey
	sta $d800+160,y
	sta $d800+240,y
	dex
	bpl l3
	tya
	bne l2

	lda #12				; set color of area for STIL text
stilcol	sta $d800+480,y
	sta $d800+664,y
	iny
	bne stilcol

	ldx #19
	ldy #0
stilco2	lda #1
	cpx #3
	bcs skip
	lda scrollcol,x
skip	sta $d800+840,x
	sta $d800+860,y
	iny
	dex
	bpl stilco2

	lda #$34
	sta 1
	ldy #0
	tya
lowmem	sta $0100,y
	sta $0200,y
	sta $0300,y
	iny
	bne lowmem

#if r1_len<=$80
	ldx #r1_len-1
copyr1	lda r1_src,x
	sta r1_org,x
	dex
	bpl copyr1
#else
	ldx #0
copyr1	lda r1_src,x
	sta r1_org,x
	inx
	cpx #r1_len
	bne copyr1
#endif
	jmp memmove


vicdata	.byte $00,$00,$00,$00,$00,$00,$00,$00
	.byte $00,$00,$00,$00,$00,$00,$00,$00
	.byte $00,$6b,$37,$00,$00,$00,$08,$00
	.byte $14,$0f,$00,$00,$00,$00,$00,$00
	.byte $00,$00,$01,$02,$03,$04,$00,$01
	.byte $02,$03,$04,$05,$06,$07

linecol	.byte 9,11,8,12,10,15,7,1,13,7,3,12,14,4,6

scrollcol .byte 11,12,15
	.)
